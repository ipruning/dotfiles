#===============================================================================
# ðŸ‘‡ 1Password
#===============================================================================
opon() {
  if [[ -z $OP_SESSION_my ]]; then
    eval "$(op signin --account my)"
  fi
}
opoff() {
  op signout
  unset OP_SESSION_my
}
# getpwd() {
#   opon
#   op get item "$1" | jq -r '.details.fields[] |select(.designation=="password").value'
#   opoff
# }
getkey() {
  opon
  op item get id_rsa_macbook_14 --format json | jq ".fields | .[1] | .value" -r | ssh-add -
  opoff
}
remkey() {
  ssh-add -D
}

#===============================================================================
# ðŸ‘‡ sudo Ctrl-S
#===============================================================================
sudo-command-line() {
  [[ -z $BUFFER ]] && zle up-history
  local cmd="sudo "
  if [[ ${BUFFER} == ${cmd}* ]]; then
    CURSOR=$(( CURSOR-${#cmd} ))
    BUFFER="${BUFFER#"$cmd"}"
  else
    BUFFER="${cmd}${BUFFER}"
    CURSOR=$(( CURSOR+${#cmd} ))
  fi
  zle reset-prompt
}
# zle     -N   sudo-command-line
# bindkey '^S' sudo-command-line

#===============================================================================
# ðŸ‘‡ Git
#===============================================================================
getrepo() {
  git clone "$1" && cd "$(basename "$1" .git)" || exit
}

#===============================================================================
# ðŸ‘‡ fzf Option-C å¿«é€ŸæŸ¥æ‰¾ç›®å½•
# ALT-C to fuzzily search for a directory in your home directory then cd into it
#===============================================================================
# if [[ $(uname) == "Darwin" ]]; then # Default #TODO, For Mac OS: Option-C
#   bindkey 'Ã§' fzf-cd-widget
# fi
export FZF_ALT_C_COMMAND="fd --ignore-file ~/.rgignore --hidden --follow --ignore-case --type d"

#===============================================================================
# ðŸ‘‡ fzf Option-X è·³è½¬è¿‘æœŸç›®å½•
#===============================================================================
fzf-dirs-widget() {
  dir=$(dirs -v | fzf --height "${FZF_TMUX_HEIGHT:-40%}" --reverse | cut -b3-)
  local dir
  if [[ -z "$dir" ]]; then
    zle redisplay
    return 0
  fi
  eval cd "${dir}"
  local ret=$?
  unset dir # ensure this doesn't end up appearing in prompt expansion
  zle reset-prompt
  return $ret
}
zle     -N    fzf-dirs-widget
# if [[ $(uname) == "Darwin" ]]; then # Default alt-X, For Mac OS: Option-X
#   bindkey 'â‰ˆ' fzf-dirs-widget
# else
#   bindkey '\ex' fzf-dirs-widget
# fi
# Use ~~ as the trigger sequence instead of the default **
# export FZF_COMPLETION_TRIGGER='~~'
export FZF_COMPLETION_OPTS='--border --info=inline'
_fzf_comprun() {
  local command=$1
  shift
  case "$command" in
    cd)           fzf "$@" --preview 'tree -C {} | head -200' ;;
    export|unset) fzf "$@" --preview "eval 'echo \$'{}" ;;
    ssh)          fzf "$@" --preview 'dig {}' ;;
    *)            fzf "$@" ;;
  esac
}

#===============================================================================
# ðŸ‘‡ fzf f
#===============================================================================
# f() {
#   sels=( "${(@f)$(fd "${fd_default[@]}" "${@:2}"| fzf)}" )
#   test -n "$sels" && print -z -- "$1 ${sels[@]:q:q}"
# }

#===============================================================================
# ðŸ‘‡ fzf æµè§ˆå™¨åŽ†å²è®°å½•
#===============================================================================
fch() {
  local cols sep google_history open
  cols=$(( COLUMNS / 3 ))
  sep='{::}'

  if [ "$(uname)" = "Darwin" ]; then
    google_history="$HOME/Library/Application Support/Google/Chrome/Default/History"
    open=open
  else
    google_history="$HOME/.config/google-chrome/Default/History"
    open=xdg-open
  fi
  cp -f "$google_history" /tmp/h
  sqlite3 -separator $sep /tmp/h \
    "select substr(title, 1, $cols), url
    from urls order by last_visit_time desc" |
  awk -F $sep '{printf "%-'$cols's  \x1b[36m%s\x1b[m\n", $1, $2}' |
  fzf --ansi --multi | sed 's#.*\(https*://\)#\1#' | xargs $open > /dev/null 2> /dev/null
}

#===============================================================================
# ðŸ‘‡ fzf æµè§ˆå™¨ä¹¦ç­¾
#===============================================================================
fcb() {
    bookmarks_path=~/Library/Application\ Support/Google/Chrome/Default/Bookmarks
    jq_script='
        def ancestors: while(. | length >= 2; del(.[-1,-2]));
        . as $in | paths(.url?) as $key | $in | getpath($key) | {name,url, path: [$key[0:-2] | ancestors as $a | $in | getpath($a) | .name?] | reverse | join("/") } | .path + "/" + .name + "\t" + .url'
    jq -r "$jq_script" < "$bookmarks_path" \
        | sed -E $'s/(.*)\t(.*)/\\1\t\x1b[36m\\2\x1b[m/g' \
        | fzf --ansi \
        | cut -d$'\t' -f2 \
        | xargs open
}

#===============================================================================
# ðŸ‘‡ fzf æ€è¿›ç¨‹
#===============================================================================
fkill() {
  (date; ps -ef) |
    fzf --bind='ctrl-r:reload(date; ps -ef)' \
        --header=$'Press CTRL-R to reload\n\n' --header-lines=2 \
        --preview='echo {}' --preview-window=down,3,wrap \
        --layout=reverse --height=80% | awk '{print $2}' | xargs kill -9
}

#===============================================================================
# ðŸ‘‡ fzf z
#===============================================================================
# z foo<tab> # shows the same completions as cd
# z foo<space><tab> # shows interactive completions via zoxide

#===============================================================================
# ðŸ‘‡ fzf asdf
#===============================================================================
# ref fasdf()

#===============================================================================
# ðŸ‘‡ tmux
#===============================================================================
tm() {
  [[ -n "$TMUX" ]] && change="switch-client" || change="attach-session"
  if [ "$1" ]; then
    tmux $change -t "$1" 2>/dev/null || (tmux new-session -d -s $1 && tmux $change -t "$1"); return
  fi
  session=$(tmux list-sessions -F "#{session_name}" 2>/dev/null | fzf --exit-0) &&  tmux $change -t "$session" || echo "No sessions found."
}
tmkill() {
    local sessions
    sessions="$(tmux ls|fzf --exit-0 --multi)"  || return $?
    local i
    for i in "${(f@)sessions}"
    do
        [[ $i =~ '([^:]*):.*' ]] && {
            echo "Killing $match[1]"
            tmux kill-session -t "$match[1]"
        }
    done
}

#===============================================================================
# ðŸ‘‡ cd
#===============================================================================
cd() {
    if [[ "$#" != 0 ]]; then
        builtin cd "$@";
        return
    fi
    while true; do
        local lsd=$(echo ".." && ls -p | grep '/$' | sed 's;/$;;')
        local dir="$(printf '%s\n' "${lsd[@]}" |
            fzf --reverse --preview '
                __cd_nxt="$(echo {})";
                __cd_path="$(echo $(pwd)/${__cd_nxt} | sed "s;//;/;")";
                echo $__cd_path;
                echo;
                ls -p --color=always "${__cd_path}";
        ')"
        [[ ${#dir} != 0 ]] || return 0
        builtin cd "$dir" &> /dev/null
    done
}

#===============================================================================
# ðŸ‘‡ https://github.com/xbin-io/xbin/
#===============================================================================
function xbin() {
  command="$1"
  args="${@:2}"
  if [ -t 0 ]; then
    curl -X POST "https://xbin.io/${command}" -H "X-Args: ${args}"
  else
    curl --data-binary @- "https://xbin.io/${command}" -H "X-Args: ${args}"
  fi
}
